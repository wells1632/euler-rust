10  REM RIGHT TRIANGLE COUNTER
20  REM ONE VERTEX ALWAYS AT ORIGIN
30  M = 50: REM MAX COORDINATE
40  C = 0: REM COUNTER
50  PRINT "COUNTING RIGHT TRIANGLES..."
60  FOR X1 = 0 TO M
70  FOR Y1 = 0 TO M
80  IF X1 = 0 AND Y1 = 0 THEN 250
90  FOR X2 = 0 TO M
100 FOR Y2 = 0 TO M
110 IF X2 = 0 AND Y2 = 0 THEN 230
120 IF X1 = X2 AND Y1 = Y2 THEN 230
130 REM CHECK RIGHT ANGLE AT ORIGIN
140 D0 = X1 * X2 + Y1 * Y2
150 REM CHECK RIGHT ANGLE AT POINT A
160 DA = - X1 * (X2 - X1) + - Y1 * (Y2 - Y1)
170 REM CHECK RIGHT ANGLE AT POINT B
180 DB = - X2 * (X1 - X2) + - Y2 * (Y1 - Y2)
190 REM IF ANY DOT PRODUCT IS ZERO, IT'S A RIGHT TRIANGLE
200 IF D0 = 0 OR DA = 0 OR DB = 0 THEN C = C + 1
210 REM PRINT PROGRESS EVERY 10000 TRIANGLES
220 IF C > 0 AND INT (C / 10000) * 10000 = C THEN PRINT C
230 NEXT Y2
240 NEXT X2
250 NEXT Y1
260 NEXT X1
270 REM DIVIDE BY 2 (COUNTED EACH TWICE)
280 C = C / 2
290 PRINT "TRIANGLES FOR 0-";M;": ";C
300 END